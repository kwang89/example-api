plugins {
    id "org.springframework.boot" version "3.0.5"
    id "io.spring.dependency-management" version "1.1.0"
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id "com.epages.restdocs-api-spec" version "0.18.0"
    id "org.ec4j.editorconfig" version "0.0.3"
    id "java"
}

group = "com.example"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_17

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    set("snippetsDir", file("build/generated-snippets"))
    set("mapstructVersion", "1.5.4.Final")
    set("guavaVersion", "31.1-jre")
    set("commonTextVersion", "1.10.0")
    set("epagesVersion", "0.18.0")
}

dependencies {

    // Spring
    // implementation "org.springframework.boot:spring-boot-starter-security"
    // testImplementation "org.springframework.security:spring-security-test"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // restdocs
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
    asciidoctorExt "org.springframework.restdocs:spring-restdocs-asciidoctor"
    testImplementation "com.epages:restdocs-api-spec-mockmvc:$epagesVersion"

    // MapStruct
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    // guava
    implementation "com.google.guava:guava:$guavaVersion"// guava
    // apache common text
    implementation "org.apache.commons:commons-text:$commonTextVersion"
    // lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    // H2
    runtimeOnly "com.h2database:h2"
}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs += [
            "-Amapstruct.defaultComponentModel=spring"
    ]
}

compileTestJava {
    options.encoding = "UTF-8"
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    configurations "asciidoctorExt"
    dependsOn test
    finalizedBy "openapi3"
    finalizedBy "postman"
}

openapi { //2.3
    host = "localhost:8080"
    title = "My API"
    description = "My API description"
    version = "1.0.0"
    format = "json"
}

// 서버 여러개 설정 시 참조
// https://github.com/ePages-de/restdocs-api-spec#gradle-plugin-configuration
// https://github.com/OAI/OpenAPI-Specification/blob/main/versions/3.0.1.md#server-object
openapi3 {
    servers = [{ url = "http://localhos:8080" }]
    title = "My API"
    description = "My API description"
    version = "0.1.0"
    format = "yaml"
}

postman {
    title = "My API"
    version = "0.1.0"
    baseUrl = "https://localhost:8080"
}

editorconfig {
    excludes = ["build"]
}

check.dependsOn editorconfigCheck
